# Find the Ogg/Vorbis libraries
# 
# Once done this will define:
#  OGGVORBIS_FOUND       - 
#  OGGVORBIS_INCLUDE_DIR - Contains all includes for the ogg/vorbis
#  OGGVORBIS_LIBRARIES   - The libraries for Ogg/Vorbis
#   OGG_LIBRARY
#   VORBIS_LIBRARY
#   VORBISFILE_LIBRARY
#   VORBISENC_LIBRARY
#   VORBIS_INCLUDE_DIR
#   OGG_INCLUDE_DIR
# 
# Environment variables usable:
#  OGG_ROOT     - OGG root location hint.
#  VORBIS_ROOT  - Vorbis root location hint.

find_path(VORBIS_INCLUDE_DIR vorbis/vorbisfile.h
	HINTS $ENV{VORBIS_ROOT}/include)
find_path(OGG_INCLUDE_DIR ogg/ogg.h
	HINTS $ENV{OGG_ROOT}/include)

find_library(OGG_LIBRARY
	NAMES ogg
	HINTS $ENV{OGG_ROOT}/lib)
find_library(VORBIS_LIBRARY
	NAMES vorbis
	HINTS $ENV{VORBIS_ROOT}/lib)
find_library(VORBISFILE_LIBRARY
	NAMES vorbisfile
	HINTS $ENV{VORBIS_ROOT}/lib)
find_library(VORBISENC_LIBRARY
	NAMES vorbisenc
	HINTS $ENV{VORBIS_ROOT}/lib)

set(OGGVORBIS_LIBRARIES ${OGG_LIBRARY} ${VORBIS_LIBRARY} ${VORBISFILE_LIBRARY} ${VORBISENC_LIBRARY})
set(OGGVORBIS_INCLUDE_DIR ${VORBIS_INCLUDE_DIR} ${OGG_INCLUDE_DIR})

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(OGGVORBIS DEFAULT_MSG
	OGGVORBIS_LIBRARIES OGGVORBIS_INCLUDE_DIR
	OGG_LIBRARY VORBIS_LIBRARY VORBISFILE_LIBRARY VORBISENC_LIBRARY VORBIS_INCLUDE_DIR OGG_INCLUDE_DIR)

mark_as_advanced(OGGVORBIS_LIBRARIES OGGVORBIS_INCLUDE_DIR
	OGG_LIBRARY VORBIS_LIBRARY VORBISFILE_LIBRARY VORBISENC_LIBRARY
	VORBIS_INCLUDE_DIR OGG_INCLUDE_DIR)
